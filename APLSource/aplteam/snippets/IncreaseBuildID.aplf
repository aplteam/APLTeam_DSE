 {r}←IncreaseBuildID fnsName;⎕ML;⎕IO;xml;row;filename;result;buildID;flag;publishConfigFilename;parentDir
⍝ Reads XML file `filename` (typically "publish.config") and reads the value of `buildid`.
⍝ If there is no such element yet it is injected with the value 0.
⍝ The build ID is then increased by 1 and the xml is written back.
⍝ Finally `fnsName` is modified accordingly and acre informed about the change.
⍝ The file "publish.config" is first searched in the current directory, then in
⍝ the parent directory. Reason is that the current directory might be the
⍝ project directory or the sub directory "Make\" in the project directory.
 ⎕IO←1 ⋄ ⎕ML←1
 r←⍬
 flag←0
 publishConfigFilename←'publish.config'
 parentDir←''
 :Repeat
     :If #.FilesAndDirs.IsFile parentDir,publishConfigFilename
         flag←1
     :Else
         parentDir←'..\'
     :EndIf
 :Until flag∨0=≢parentDir
 filename←parentDir,'publish.config'
 xml←⎕XML'flat'#.APLTreeUtils2.ReadUtf8File filename
 :If ~(⊂'buildid')∊xml[;2]
     xml⍪←1 'buildid' '0'(0 2⍴⊂'')5
 :EndIf
 row←xml[;2]⍳⊂'buildid'
 buildID←1+⊃(//)⎕VFI 3⊃xml[row;]
 xml[row;3]←⊂⍕buildID
 (⊂⎕XML xml)⎕NPUT filename
 fnsName AddBuildID2Version buildID
 result←⎕SE.UCMD'acre.SetChanged ',⍕fnsName
 Assert 1=⍴result
⍝Done
